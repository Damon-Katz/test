# Pong

Game elements 
  #balls = [|x  y   vx vy radius color  a clicked| 
            50  10  0  0  10     0xA00000 1 false
            100 100 0  30 30     0xA00000 1 false  ]
  #gravity = 0.1

Add a game timer
  #time/timer += [period: 16<ms>]

## Movement

Move the ball with every tick of the timer
  ~ #time/timer.ticks
  #balls.x := #balls.x + #balls.vx
  #balls.y := #balls.y + #balls.vy
  #balls.vy := #balls.vy + #gravity * #balls.a

Keep the balls within the boundary height
  iy = #balls.y > 500
  iyy = #balls.y < 0
  #balls.y{iy} := 500
  #balls.y{iyy} := 0
  #balls.vy{iy | iyy} := #balls.vy * -0.80

Keep the balls within the boundary width
  ix = #balls.x > 500
  ixx = #balls.x < 0
  #balls.x{ix} := 500
  #balls.x{ixx} := 0
  #balls.vx{ix | ixx} := #balls.vx * -0.80

Determine if the pointer is within the ball's hitbox
  dx = #html/event/pointer-move.x - #balls.x
  dy = #html/event/pointer-move.y - #balls.y
  #hover = ((dx ^ 2 + dy ^ 2) ^ 0.5) < #balls.radius

Change color of hovered balls
  #balls.color{#hover} := 0x00A000
  #balls.color{Â¬#hover} := 0xA00000

Click the circle
  ~ #html/event/pointer-down
  #balls.clicked{#hover} := true
  #balls.a{#hover} := 0
  #balls.x{#hover} := #html/event/pointer-down.x
  #balls.y{#hover} := #html/event/pointer-down.y
  #balls.vy{#hover} := 0
  #balls.vx{#hover} := 0

Un-click the circle
  ~ #html/event/pointer-up
  #balls.clicked{#hover} := false
  #balls.a{#hover} := 1

Move the circle to the pointer if it's clicked
  ~ #html/event/pointer-move
  ix = #balls.clicked
  #balls.x{ix} := #html/event/pointer-move.x
  #balls.y{ix} := #html/event/pointer-move.y

Move the ball with the keyboard
  ~ #html/event/key-down
  up = #html/event/key-down.key == "ArrowUp"
  down = #html/event/key-down.key == "ArrowDown"
  left = #html/event/key-down.key == "ArrowLeft"
  right = #html/event/key-down.key == "ArrowRight"
  #balls.vx{left} := -5
  #balls.vx{right} := 5
  #balls.vy{up} := -5
  #balls.vy{down} := 5

Set up drawing elements 
  #circle = [
    shape: "circle" 
    parameters: [
      center-x: #balls.x 
      center-y: #balls.y 
      radius: #balls.radius
      fill: #balls.color 
      line-width: 3
    ]
  ]

Draw to canvas
  #canvas = [
    type: "canvas" 
    contains: [|shape parameters| #circle] 
    parameters: [width: 1000 height: 500]
  ]

Attach game to root
  #html/app = [
    root: "mech-root" 
    contains: [|type contains parameters| #canvas]
  ]