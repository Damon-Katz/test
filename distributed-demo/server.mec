# Mech Demo - Server

Game elements 
  #balls = [|x   y   vx vy radius clicked| 
             50  10  0  0  10     1
             100 100 0  30 30     1      ]
  #gravity = 0.1

## Motion Model

Move the ball with every tick of the timer
  ~ #time/timer.ticks
  #balls.x := #balls.x + #balls.vx
  #balls.y := #balls.y + #balls.vy
  #balls.vy := #balls.vy + #gravity * #balls.clicked

Keep the balls within the boundary height
  ~ #time/timer.ticks
  iy = #balls.y > 500
  iyy = #balls.y < 0
  #balls.y{iy} := 500
  #balls.y{iyy} := 0
  #balls.vy{iy | iyy} := #balls.vy * -0.80

Keep the balls within the boundary width
  ~ #time/timer.ticks
  ix = #balls.x > 650
  ixx = #balls.x < 0
  #balls.x{ix} := 650
  #balls.x{ixx} := 0
  #balls.vx{ix | ixx} := #balls.vx * -0.80